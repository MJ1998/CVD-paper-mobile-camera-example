// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
	repositories {
		google()
	}
	dependencies {
		def nav_version = "2.5.3"
		classpath("androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version")
	}
}

plugins {
	id 'com.android.application' version '8.0.0-rc01' apply false
	id 'com.android.library' version '8.0.0-rc01' apply false
	id 'org.jetbrains.kotlin.android' version '1.8.10' apply false
	id 'com.diffplug.spotless' version '6.6.0'
}

spotless {
	kotlin {
		target("**/*.kt")
		targetExclude("**/build/")
		targetExclude("**/*_Generated.kt")
		ktlint()
		ktfmt().googleStyle()
		licenseHeaderFile(
				"${project.rootProject.projectDir}/license-header.txt",
				"package|import|class|object|sealed|open|interface|abstract "
				// It is necessary to tell spotless the top level of a file in order to apply config to it
				// See: https://github.com/diffplug/spotless/issues/135
		)
	}
	java {
		target("**/*.java")
		targetExclude("**/build/")
		targetExclude("**/*_Generated.java")
		googleJavaFormat()
		licenseHeaderFile(
				"${project.rootProject.projectDir}/license-header.txt",
				"package|import|class|object|sealed|open|interface|abstract "
				// It is necessary to tell spotless the top level of a file in order to apply config to it
				// See: https://github.com/diffplug/spotless/issues/135
		)
	}
	format("gradle") {
		target("**/*.gradle")
		trimTrailingWhitespace()
		indentWithTabs()
	}
	format("xml") {
		target("**/*.xml")
		targetExclude("**/build/", ".idea/")
		prettier(["prettier": "2.0.5", "@prettier/plugin-xml": "0.13.0"])
				.config(["parser": "xml", "tabWidth": 4])
	}
}
